% Church style STLC.
metavar term_var, x, y, z, f, u, v ::=
metavar type_var, X ::=
indexvar i ::= 

grammar

type, T :: 'Type_' ::=
  | X       ::   :: Base
    {{ tex \mathsf{X} }}
  | T -> T' ::   :: Arrow
    {{ tex [[T]] \to [[T']] }}
  | ( T )   :: S :: Parens

term, t :: 'Term_' ::= 
  | x              ::   :: Variables
  | \ x : T . t    ::   :: Lam (+ bind x in t +)
    {{ tex \lambda [[x]] : [[T]] . [[t]] }}
  | t1 t2          ::   :: App
  | [ t / x ] t'   :: M :: Substitution
  | ( t )          :: S :: Parens

G {{ tex \Gamma }} :: 'Context_' ::=
  | x : T       :: :: Element
  | .           :: :: Empty
    {{tex \cdot }}
  | G , G'      :: :: Extension

formula :: 'formula_' ::=
  | judgement   ::    :: judgement
  | ( formula ) ::    :: parens
  | G == G'     :: M  :: CtxDef
  {{tex [[G]] \defeq [[G']] }}

defns
  JTyping :: '' ::=
  defn
  G |- t : T :: :: typing :: '' 
    {{ tex [[G]] \vdash [[t]] : [[T]] }}
  by
  
  --------------------- :: Var
  G, x : T, G' |- x : T

  G, x : T1 |- t : T2
  ----------------------- :: Lam
  G |- \x:T1.t : T1 -> T2

  G |- t1 : T1 -> T2
  G |- t2 : T1
  ------------------ :: App
  G |- t1 t2 : T2

defns
 Opsem :: '' ::=
 defn
 t ~> t' :: :: opsem :: 'R_'
   {{tex [[t]] \redto [[t']] }}
 by
  
  ----------------------- :: Beta
  (\x : T . t) t' ~> [t'/x]t  

  t ~> t'
  ----------------- :: Lam
  \x:T.t ~> \x:T.t'

  t1 ~> t'1
  --------------- :: App1
  t1 t2 ~> t'1 t2

  t2 ~> t'2
  --------------- :: App2
  t1 t2 ~> t1 t'2
