% Stratified System F.
metavar term_var, x, y, z, f, u, v ::=
metavar type_var, X, Y ::= 
indexvar level_var, l ::= 
         
grammar

kind, p,q :: 'Kind_' ::=
  | l                  ::  :: Level
  | p + 1              ::  :: LevelUp
  | max ( p , q )     ::  :: Max
    {{ tex \mathsf{max} ( [[p]] , [[q]] ) }}

type, T  :: 'Type_' ::=
  | X            ::   :: Var
  | T -> T'      ::   :: Arrow
    {{ tex [[T]] \to [[T']] }}
  | Forall X : p . T ::   :: Forall
    {{ tex \forall [[X]]:*_{[[p]]} . [[T]] }}
  | ( T )        :: S :: Parens
  | [ T / X ] T' :: M :: Substitution

term, t :: 'Term_' ::= 
  | x              ::   :: Variables
  | \ x : T . t    ::   :: Lam (+ bind x in t +)
    {{ tex \lambda [[x]] : [[T]] . [[t]] }}
  | \\ X : p . t       ::   :: TypeAbs
    {{ tex \Lambda [[X]] : *_{[[p]]} . [[t]] }}
  | t1 t2          ::   :: App
  | t [ T ]        ::   :: TypeApp
  | [ t / x ] t'   :: M :: Substitution
  | [ T / X ] t    :: M :: TypeSubstitution
  | ( t )          :: S :: Parens

G {{ tex \Gamma }} :: 'Context_' ::=
  | x : T       :: :: TermElement
  | X : p       :: :: TypeElement
  {{tex [[X]] : *_{[[p]]} }}
  | .           :: :: Empty
  | G , G'      :: :: Extension

formula :: 'formula_' ::=
  | judgement   ::  :: judgement
  | ( formula ) ::  :: parens

defns
  JTyping :: '' ::=
  defn
  G |- T : p :: :: kinding :: 'K_'
    {{ tex [[G]] \vdash [[T]] : *_{[[p]]} }}
  by

  --------------------- :: Var
  G, X : p, G' |- X : p

  G |- T1 : p
  G |- T2 : q
  ------------------------- :: Arrow
  G |- T1 -> T2 : max(p,q)

  G, X:p |- T : q
  ------------------------------- :: Forall
  G |- Forall X:p.T : max(p+1,q)

defns
 Opsem :: '' ::=
 defn
 t ~> t' :: :: opsem :: 'R_'
   {{tex [[t]] \redto [[t']] }}
 by
  
  -------------------------- :: Beta
  (\x : T . t) t' ~> [t'/x]t  

  ---------------------- :: TypeRed
  (\\X:p.t)[T] ~> [T/X]t

  t ~> t'
  ----------------- :: Lam
  \x:T.t ~> \x:T.t'

  t ~> t'
  ------------------- :: TypeAbs
  \\X:p.t ~> \\X:p.t'

  t1 ~> t'1
  --------------- :: App1
  t1 t2 ~> t'1 t2

  t2 ~> t'2
  --------------- :: App2
  t1 t2 ~> t1 t'2

  t ~> t'
  ------------- :: TypeApp
  t[T] ~> t'[T]

  defn
  G |- t : T :: :: typing :: '' 
    {{ tex [[G]] \vdash [[t]] : [[T]] }}
  by

  G |- T : p  
  --------------------- :: Var
  G, x : T, G' |- x : T

  G, x : T1 |- t : T2
  ----------------------- :: Lam
  G |- \x:T1.t : T1 -> T2

  G |- t1 : T1 -> T2
  G |- t2 : T1
  ------------------ :: App
  G |- t1 t2 : T2

  G, X:p |- t : T
  ------------------------------- :: TypeAbs
  G |- \\X : p .t : Forall X:p.T

  G |- t : Forall X:p.T'
  G |- T : p
  ---------------------- :: TypeApp
  G |- t[T] : [T/X]T'

